<?xml version="1.0"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
               xmlns:mx="library://ns.adobe.com/flex/mx"
               xmlns:popup="pl.znr.heatmaster.ui.components.popup.*"
               xmlns:components="pl.znr.heatmaster.ui.components.*"
               width="100%"
               height="100%"
               xmlns:s="library://ns.adobe.com/flex/spark"
               skinClass="pl.znr.heatmaster.ui.components.skins.CustomSparkApplicationSkin"
               xmlns:tooltip="pl.znr.heatmaster.ui.components.tooltip.*"
               creationComplete="appInitialized()"
               initialize="initHeatMaster()"
               pageTitle="HeatMaster"
               preloader="pl.znr.heatmaster.CustomPreloader"

        >




    <fx:Metadata>
        [ResourceBundle("hm")]
    </fx:Metadata>

    <fx:Metadata>
        [Event(name="langChange", type="flash.events.Event")]
    </fx:Metadata>

    <fx:Declarations>

        <fx:XML id="configXML" format="e4x" source="/config.xml"/>

        <s:BitmapFill id="bgGrass" source="@Embed('/pl/znr/heatmaster/assets/images/backgrounds/background_grass.png')"/>
        <s:BitmapFill id="bgSky" source="@Embed('/pl/znr/heatmaster/assets/images/backgrounds/background_sky.png')"/>

    </fx:Declarations>


    <fx:Script><![CDATA[
        import mx.collections.ArrayCollection;
        import mx.controls.Alert;
        import mx.controls.ToolTip;
        import mx.core.FlexGlobals;
        import mx.core.FlexLoader;
        import mx.core.IToolTip;
        import mx.core.IVisualElementContainer;
        import mx.core.UIComponent;
        import mx.events.ToolTipEvent;
        import mx.formatters.DateFormatter;
        import mx.managers.BrowserManager;
        import mx.managers.ToolTipManager;

        import pl.znr.heatmaster.HeatMaster;

        import pl.znr.heatmaster.config.CountryItem;
        import pl.znr.heatmaster.config.CountryItemHelper;
        import pl.znr.heatmaster.config.CountryItemHelper;
        import pl.znr.heatmaster.config.CountryItemHelper;
        import pl.znr.heatmaster.config.CountryItemHelper;
        import pl.znr.heatmaster.config.CountryItemHelper;
        import pl.znr.heatmaster.config.CountryItemHelper;
        import pl.znr.heatmaster.config.CountryItemHelper;
        import pl.znr.heatmaster.config.CountryItemHelper;
        import pl.znr.heatmaster.config.CountryItemHelper;

        import pl.znr.heatmaster.config.HeatMasterConfigurationReader;
        import pl.znr.heatmaster.config.RegionItem;
        import pl.znr.heatmaster.constants.combo.ConversionUnits;

        import pl.znr.heatmaster.core.CachedDataContextManager;
        import pl.znr.heatmaster.core.DataContext;

        import pl.znr.heatmaster.core.HeatMasterController;
        import pl.znr.heatmaster.core.IBuildingAgeChangeListener;
        import pl.znr.heatmaster.core.model.EnvironmentalData;
        import pl.znr.heatmaster.core.model.InsolationData;
        import pl.znr.heatmaster.locale.HeatMasterLocaleManager;
        import pl.znr.heatmaster.locale.LangHelper;
        import pl.znr.heatmaster.ui.HeatMasterChangeListener;
        import pl.znr.heatmaster.ui.builder.prepare.EnvironmentalDataPreparer;
        import pl.znr.heatmaster.ui.components.HeatMasterComponent;
        import pl.znr.heatmaster.ui.components.HeatMasterResultComponent;
        import pl.znr.heatmaster.ui.components.HouseStandardChangeListener;
        import pl.znr.heatmaster.ui.components.LangChangeListener;

        import spark.utils.TextFlowUtil;

        public static const CURRENCY_EURO_LOCALE_CODE:String = 'hm.currency.euro';
        public static const YEAR_COST_SUFFIX_CODE:String = 'year_cost';
        public static const MONTH_COST_SUFFIX_CODE:String = 'month_cost';

        private var groundBackgroundImgSrc:String = "pl/znr/heatmaster/assets/images/backgrounds/background_grass.png";
        private var skyBackgroundImgSrc:String = "pl/znr/heatmaster/assets/images/backgrounds/background_sky.png";

        private var heatMasterListener:HeatMasterChangeListener = new HeatMasterChangeListener();
        private var heatMasterController:HeatMasterController = new HeatMasterController();
        private var cachedDataContextManager:CachedDataContextManager = new CachedDataContextManager();
        private var environmentalDataPreparer:EnvironmentalDataPreparer = new EnvironmentalDataPreparer();
        private var configurationReader:HeatMasterConfigurationReader;
        private var heatMasterLocaleManager:HeatMasterLocaleManager;

        private var cachedData:DataContext;

        [Bindable]
        private var startButtonVisible:Boolean = true;

        [Bindable]
        private var plotVisible:Boolean = false;

        private var footerTip:IToolTip;
        private var myFooterText:RichText;

        private var localeToggled:Boolean = false;
        private var langChangeListeners:ArrayCollection = new ArrayCollection();
        private var configItems:ArrayCollection;

        private var selectedLang:String;

        public static function getInstance():HeatMaster {
            return FlexGlobals.topLevelApplication as HeatMaster;
        }

        public function getHousePanel():HousePanel {
            return housePanel;
        }

        public function addToolTip(x:int, y:int, toolTipComp:UIComponent):void {
            toolTipComp.x = x;
            toolTipComp.y = y;
            addElement(toolTipComp);
        }

        private function initHeatMaster():void {
            trace("Initializing HeatMaster...");
            heatMasterLocaleManager = new HeatMasterLocaleManager(resourceManager);
            configurationReader = new HeatMasterConfigurationReader(configXML, resourceManager);
            configItems = configurationReader.readConfig();
        }

        private function setPolishComponentsVisibility(polishVisible:Boolean):void {
            logoGroup.visible = polishVisible;
            houseTopBarImg.visible = !polishVisible;
            logoImage.visible = !polishVisible;
            houseTopBarWhiteImg.visible = polishVisible;
            helpBtn.visible = polishVisible;
            techBtn.visible = polishVisible;
            aboutBtn.visible = polishVisible;
        }

        private function getConfigItemCode(cachedData:DataContext):String {
            if (cachedData != null && cachedData.selectedCountryRegion != null) {
                return cachedData.selectedCountryRegion;
            }
            return CountryItemHelper.getInitialConfigItemCode();
        }

        private function showLogoTip():void {
            if (myFooterText == null) {
                myFooterText = createFooterText();
            }
            addElement(myFooterText);
        }

        private function hideLogoToolTip():void {
            removeElement(myFooterText);
        }

        private function destroyLogoTip(event:Object):void {
            ToolTipManager.destroyToolTip(footerTip);
        }

        private function getFooterText():RichText {
            if (myFooterText == null) {
                myFooterText = createFooterText();
            }
            return myFooterText;
        }

        private function createFooterText():RichText {
            var richText:RichText = new RichText();

            var text:String = "<p>HeatMaster został opracowany w ramach kampanii Ubierz Swój Dom</p>\
                               <p>dofinansowanej ze środków Narodowego Funduszu Ochrony Środowiska</p>\
                               <p>i Gospodarki Wodnej oraz Wojewódzkiego Funduszu Ochrony Środowiska</p>\
                               <p>i Gospodarki Wodnej w Katowicach</p>";

            richText.textFlow = TextFlowUtil.importFromString(text);
            richText.right = 5;
            richText.bottom = 5;
            richText.setStyle("color", "white");
            richText.setStyle("fontSize", "8");
            richText.setStyle("verticalAlign", "bottom");
            return richText;
        }


        override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void {
            // plotPanel.width = Math.max(1000,1200/1280 * this.width)
            super.updateDisplayList(unscaledWidth, unscaledHeight);
            //Alert.show("scroller width: " + scroller.width);
        }

        private function appInitialized():void {
            BrowserManager.getInstance().setFragment("HeatMaster");
            BrowserManager.getInstance().setTitle("HeatMaster");


            setUpHeatMasterListener();
            injectHeatMasterListener(heatMasterContainer, heatMasterListener);
            setUpOtherListeners(heatMasterContainer,heatMasterListener);

            cachedData = cachedDataContextManager.readCache();

            var countryRegionCode:String = CountryItemHelper.getInitialConfigItemCode();
            var regionItem:RegionItem = CountryItemHelper.getRegionByCodeFromCountryList(configItems, countryRegionCode);
            var countryItem:CountryItem = regionItem != null ? regionItem.country : CountryItemHelper.getCountryByCode(configItems, countryRegionCode);

            housePopup.populateCountryRegionDropDownList(configItems, regionItem, countryItem, countryRegionCode);
            heatingPopup.configChanged(countryItem, true);
            housePanel.energyMeter.countryChanged(countryItem);
            housePanel.energyMeter.setConfigurationReader(configurationReader);

            startButtonVisible = cachedData == null;

            var language:String = cachedData != null ? cachedData.lang : null;
            if(language == null){
               language = heatMasterLocaleManager.getLang();
            }
            if(!LangHelper.isPolishLang(language)){
              setPolishComponentsVisibility(false);
            }
            resourceManager.localeChain = [language];
            initialPhaseNotifyLangChangeListeners(language);
            selectedLang = language;
        }

        private function setUpHeatMasterListener():void {
            heatMasterListener.setHeatMasterController(heatMasterController);
            heatMasterListener.setConfigurationReader(configurationReader);
            heatMasterListener.setCacheManager(cachedDataContextManager);
            heatMasterListener.setHousePanel(housePanel);
            heatMasterListener.setHeatingPopup(heatingPopup);
            heatMasterListener.setHousePopup(housePopup);
            heatMasterListener.setEnergyMeter(housePanel.energyMeter);
        }

        private function setUpOtherListeners(component:Object, hmListener:HeatMasterChangeListener):void {
            if (component is IBuildingAgeChangeListener) {
                hmListener.addBuildingChangeListener(component as IBuildingAgeChangeListener);
            }
            if(component is HouseStandardChangeListener){
               hmListener.addHouseStandardChangeListener(component as HouseStandardChangeListener)
            }
            if (component is LangChangeListener) {
                langChangeListeners.addItem(component as LangChangeListener);
            }
            if (component is IVisualElementContainer) {
                var visualElementContainer:IVisualElementContainer = component as IVisualElementContainer;
                for (var i:int = 0; i < visualElementContainer.numElements; i++) {
                    setUpOtherListeners(visualElementContainer.getElementAt(i), hmListener);
                }
            }
        }

        private function findAndAddBuildingAgeListeners(component:Object, hmListener:HeatMasterChangeListener):void {
            if (component is IBuildingAgeChangeListener) {
                hmListener.addBuildingChangeListener(component as IBuildingAgeChangeListener);
            }
            if (component is IVisualElementContainer) {
                var visualElementContainer:IVisualElementContainer = component as IVisualElementContainer;
                for (var i:int = 0; i < visualElementContainer.numElements; i++) {
                    findAndAddBuildingAgeListeners(visualElementContainer.getElementAt(i), hmListener);
                }
            }
        }

        private function setupLangChangeListeners(component:Object):void {
            if (component is LangChangeListener) {
                langChangeListeners.addItem(component as LangChangeListener);
            }
            if (component is IVisualElementContainer) {
                var visualElementContainer:IVisualElementContainer = component as IVisualElementContainer;
                for (var i:int = 0; i < visualElementContainer.numElements; i++) {
                    setupLangChangeListeners(visualElementContainer.getElementAt(i));
                }
            }
        }

        private function injectHeatMasterListener(component:Object, hmListener:HeatMasterChangeListener):void {
            if (component is HeatMasterComponent) {
                var hmComponent:HeatMasterComponent = component as HeatMasterComponent;
                hmComponent.setHeatMasterListener(hmListener);
                heatMasterController.addDataContextListener(hmComponent);
            }
            if (component is HeatMasterResultComponent) {
                var hmResultComponent:HeatMasterResultComponent = component as HeatMasterResultComponent;
                heatMasterController.addResultDataListener(hmResultComponent);
            }
            if (component is IVisualElementContainer) {
                var visualElementContainer:IVisualElementContainer = component as IVisualElementContainer;
                for (var i:int = 0; i < visualElementContainer.numElements; i++) {
                    injectHeatMasterListener(visualElementContainer.getElementAt(i), hmListener);
                }
            }
        }

        private function continueCalculation():void {
            setStartViewState();
            if (cachedData != null) {

                var regionItem:RegionItem = CountryItemHelper.getInitialOrCachedRegionItem(true, cachedData, configItems);
                var countryItem:CountryItem = regionItem != null ? regionItem.country : CountryItemHelper.getInitialOrCachedCountryItem(true, cachedData, configItems);

                var conversionUnit:int = cachedData.conversionData.selectedUnit;

                heatingPopup.configChanged(countryItem, cachedData.localCurrency);
                housePanel.energyMeter.countryChanged(countryItem);

                heatMasterController.calculateAndSetViewState(cachedData);

                heatMasterContainer.removeElement(introGroup);
                heatMasterContainer.removeElement(housePopup);

                housePopup.setRolledBackState();
                heatingPopup.setRolledBackState();
                heatMasterContainer.removeElement(blurringImg);

                heatMasterContainer.addElement(housePopup);
                heatMasterListener.setCacheReady(true);
                heatMasterListener.languageChanged(selectedLang);
            }
            notifyLangChangeListeners(selectedLang);

        }

        private function newCalculation():void {
            setStartViewState();

            var initDataContext:DataContext = initDataContextWithConfig();

            initDataContext = heatMasterController.initData(initDataContext);
            if(initDataContext.houseData.standardType != null){
               initDataContext = heatMasterListener.applyHouseStandardChanged(initDataContext,initDataContext.houseData.standardType);
            }

            heatMasterController.startCalculationWithConfig(initDataContext);

            heatMasterContainer.removeElement(introGroup);
            heatMasterContainer.removeElement(housePopup);
            housePopup.rollDownStartupPopup();
            heatMasterContainer.addElement(housePopup);

            heatMasterListener.languageChanged(selectedLang);
            notifyLangChangeListeners(selectedLang);
        }

        private function initDataContextWithConfig():DataContext {

            var regionItem:RegionItem = CountryItemHelper.getInitialOrCachedRegionItem(false, null, configItems);
            var countryItem:CountryItem = regionItem != null ? regionItem.country : CountryItemHelper.getInitialOrCachedCountryItem(false, null, configItems);

            var dataContext:DataContext = new DataContext();
            var envData:EnvironmentalData = new EnvironmentalData();
            var insolationData:InsolationData = new InsolationData();
            if (regionItem != null) {
                envData.temperatures = regionItem.temperatures;
                envData.groundTemperatures = regionItem.groundTemperatures;
                insolationData.groundInsolation45 = regionItem.optimalInsolation;
                insolationData.northInsolation = regionItem.insolationN;
                insolationData.southInsolation = regionItem.insolationS;
                insolationData.westEastInsolation = regionItem.insolationWE;
            }
            else {
                envData.temperatures = countryItem.temperatures;
                envData.groundTemperatures = countryItem.groundTemperatures;
                insolationData.groundInsolation45 = countryItem.optimalInsolation;
                insolationData.northInsolation = countryItem.insolationN;
                insolationData.southInsolation = countryItem.insolationS;
                insolationData.westEastInsolation = countryItem.insolationWE;
            }
            envData.insolationData = insolationData;
            envData = environmentalDataPreparer.prepare(envData);
            dataContext.environmentalData = envData;
            dataContext.initialConfigItem = countryItem;
            dataContext.selectedCountryRegion = regionItem != null ? regionItem.code : countryItem.code;
            return dataContext;
        }

        private function setStartViewState():void {
            logoGroup.visible = false;
            menuButtonGroup.visible = true;
            housePopup.visible = true;
            heatingPopup.visible = true;
            houseTopBarImg.visible = true;
            houseTopBarWhiteImg.visible = false;
            logoImage.visible = true;
        }

        public function goToHeatingPopupState():void {
            heatMasterContainer.removeElement(blurringImg);
            heatMasterContainer.addElement(blurringImg);
            heatMasterContainer.removeElement(heatingPopup);
            heatingPopup.rollDownStartupPopup();
            heatMasterContainer.addElement(heatingPopup);
        }

        public function goToNormalState():void {
            try {
                tryWriteCache();
                heatMasterContainer.removeElement(blurringImg);
            } catch (e:Error) {
                Alert.show("Error writing cache...", e.message);
            }
        }

        public function groundBackgroundVisible(visible:Boolean):void {
            bgGrassImg.visible = visible;
            bgSkyImg.visible = !visible;
        }

        private function tryWriteCache():void {
            var dataContext:DataContext = heatMasterController.getDataContext();
            if (dataContext != null) {
                cachedDataContextManager.writeCache(dataContext);
                heatMasterListener.setCacheReady(true);
            }
        }

        private function reportClicked():void {
            plotVisible = !plotVisible;
        }

        private function openManualPdf():void {
            var url:String = "http://ziemianarozdrozu.pl/dl/heatmaster/HeatMaster-Manual-PL.pdf";
            navigateToURL(new URLRequest(url), "_blank");
        }

        private function openTechPdf():void {
            var url:String = "http://ziemianarozdrozu.pl/dl/heatmaster/HeatMaster-Tech-PL.pdf";
            navigateToURL(new URLRequest(url), "_blank");
        }

        private function openAboutPdfFile():void {
            var url:String = "http://ziemianarozdrozu.pl/dl/heatmaster/HeatMaster-Team-PL.pdf";
            navigateToURL(new URLRequest(url), "_blank");
        }

        private function goToFER():void {
            var url:String = "http://fer.org.pl";
            navigateToURL(new URLRequest(url), "_blank");
        }

        private function goToARKA():void {
            var url:String = "http://www.fundacjaarka.pl/";
            navigateToURL(new URLRequest(url), "_blank");
        }

        private function goToNFOS():void {
            var url:String = "http://nfosigw.gov.pl";
            navigateToURL(new URLRequest(url), "_blank");
        }

        private function goToWFOS():void {
            var url:String = "http://www.wfosigw.katowice.pl/";
            navigateToURL(new URLRequest(url), "_blank");
        }

        private function getIconsOffset():int {
            if (this.width <= 1154) {
                return Math.max(12, 16 * this.width / 1280)
            }

            return Math.min(20, Math.max(0, (this.width - 1024) / 256 * 20));
        }

        private function getReportIconOffset():int {
            return Math.min(20, Math.max(0, (this.width - 1024) / 256 * 20));
        }

        private function clearCache():void {
            cachedDataContextManager.clearCache();
        }

        public function hideReport():void {
            plotVisible = false;
        }

        private function getLogoWidth():Number {
            return this.width < 1280 ? 120 : 200;
        }

        private function getLogoHeight():Number {
            return this.width < 1280 ? 46 * 0.6 : 46;
        }

        private function toggleLocale():void {
            localeToggled = !localeToggled;
            selectedLang = localeToggled ? LangHelper.US_LANG_CODE : LangHelper.PL_LANG_CODE;
            resourceManager.localeChain = localeToggled ? [selectedLang] : [selectedLang];
            this.dispatchEvent(new Event("langChange"));
            notifyLangChangeListeners(selectedLang);
            heatMasterListener.languageChanged(selectedLang);
        }

        private function notifyLangChangeListeners(lang:String):void {
            trace("Notifying lang change listeners: " + lang);
            if (lang != null) {
                for (var i:int = 0; i < langChangeListeners.length; i++) {
                    var langChangeListener:LangChangeListener = langChangeListeners.getItemAt(i) as LangChangeListener;
                    langChangeListener.langChanged(lang);
                }
            }
        }

        private function initialPhaseNotifyLangChangeListeners(lang:String):void {
            if (lang != null) {
                for (var i:int = 0; i < langChangeListeners.length; i++) {
                    var langChangeListener:LangChangeListener = langChangeListeners.getItemAt(i) as LangChangeListener;
                    langChangeListener.initialLangChanged(lang);
                }
            }
        }

        private function setEnglishLang():void {
            selectedLang = "en_US";
            setPolishComponentsVisibility(false);
            resourceManager.localeChain = ["en_US"];
            initialPhaseNotifyLangChangeListeners(selectedLang);
        }

        private function setPolishLang():void {
            selectedLang = "pl_PL";
            setPolishComponentsVisibility(false);
            resourceManager.localeChain = ["pl_PL"];
            initialPhaseNotifyLangChangeListeners(selectedLang);
        }

        ]]></fx:Script>


    <fx:Style>
        @namespace s "library://ns.adobe.com/flex/spark";
        s|DropDownList {
            fontSize:10;
            fontWeight:bold;
            color:gray;
        }


        .popupPanelLabelOrangeStyle {
            fontSize: 12px;
            color: #FF6600;
            verticalAlign: middle;
            fontWeight: bold;
        }

        .popupPanelHeader {
            fontSize: 13px;
            color: #FF6600;
            verticalAlign: middle;
            fontWeight: bold;
        }

        .baloonHeader {
            fontSize: 12px;
            verticalAlign: middle;
            fontWeight: bold;
            color: #333333;
        }

        .pupupPanelGrayFontStyle {
            fontSize: 11px;
            color: #333333;
            fontWeight: bold;
        }

        .smallPupupPanelGrayFontStyle {
            fontSize: 11px;
            color: gray;
            fontWeight: bold;
        }

        .checkBoxStyle {
            borderColor: #FF6600;
        }

        .arrowLabelStyle {
            color: #FF6600;
            fontSize:15px;
            fontWeight:bold;
            textAlign: center;
            verticalAlign: middle;
        }

        .verticalAxisStyle {
            color: blue;
            fontSize: 11px;
            fontWeight: bold;
            textAlign: right;
        }

        .reportBlackLabel {
            fontSize: 12px;
            fontWeight: bold;
            color: black;
        }

        .reportDarkGrayLabel {
            fontSize: 12px;
            fontWeight: bold;
            color: #a9a9a9;
        }

        .reportGrayLabel {
            fontSize: 12px;
            fontWeight: bold;
            color: gray;
        }

        .reportLegendVerticalLabel {
            fontSize: 12px;
            fontWeight: bold;
            color: black;
            verticalAlign: middle;
        }

        .monthLegendStyle {
            fontSize: 12px;
            fontWeight: bold;
            color: black;
        }

        .monthLegendValueStyle {
            fontSize: 11px;
            fontWeight: bold;
            color: red;
        }


    </fx:Style>

    <!--<mx:Canvas width="100%" height="100%" backgroundAlpha="10" >-->
    <!--<popup:HousePopup y="10" x="10" />-->
    <!--<popup:HeatingPopup y="300" x="10" width="320" height="150"/>-->
    <!--<popup:VentilationPupup x="500" y="50"/>-->
    <!--</mx:Canvas>-->



    <s:Scroller id="scroller" width="100%" >
        <s:Group >
            <s:BorderContainer x="0" id="heatMasterContainer" width="{Math.max(1000,this.width * 0.9)}"  height="715" borderWeight="0">

                <mx:Image id="bgGrassImg" width="{Math.max(1024,this.width )}" height="715" visible="true"  maintainAspectRatio="false" source="@Embed('/pl/znr/heatmaster/assets/images/backgrounds/background_grass.png')"/>
                <mx:Image id="bgSkyImg" width="{Math.max(1024,this.width )}" height="715"  visible="false" maintainAspectRatio="false" source="@Embed('/pl/znr/heatmaster/assets/images/backgrounds/background_sky.png')"/>

                <mx:Image width="{Math.max(1024,this.width )}" height="47" maintainAspectRatio="false" x="0" y="0" id="houseTopBarImg"
                          source="@Embed('/pl/znr/heatmaster/assets/images/top_bar.png')" visible="false"/>


                <mx:Image id="logoImage" x="20" y="10" source="@Embed('/pl/znr/heatmaster/assets/images/logo.png')" visible="false"/>



                <components:HousePanel x="{Math.max(0,this.width - 1000)/2}" id="housePanel" width="1000" height="715" visible="{!plotVisible}"/>
                <components:PlotPanel  x="{Math.max(0,(this.width - plotPanel.width)/2)}" maxWidth="1200" y="90" height="560" width="{Math.min(Math.max(1000 * 1200/1280,1200/1280 * this.width),1200)}"
                                       id="plotPanel" visible="{plotVisible}"/>


                <popup:HousePopup id="housePopup" y="18" x="{housePanel.x + 200}" height="400" visible="false" />
                <popup:HeatingPopup id="heatingPopup" y="18" x="{housePopup.x + housePopup.width}" visible="false"/>



                <s:Group id="menuButtonGroup" y="0" x="{heatingPopup.x + heatingPopup.width + getReportIconOffset()}" visible="false">
                    <mx:Button id="reportBtn" x="0"  y="14" toolTip="{resourceManager.getString('hm','hm.tooltip.button.report')}"
                               width="{Math.min(27 * this.width/1280,27)}" maxWidth="27" minWidth="21"
                               height="{Math.min(28 * this.width/1280,28)}" maxHeight="28" minHeight="21"
                               skin="@Embed('/pl/znr/heatmaster/assets/images/icons/raport_icon.png')"
                               overSkin="@Embed('/pl/znr/heatmaster/assets/images/icons/raport_icon_over.png')"
                               buttonMode="true">
                        <mx:click><![CDATA[this.reportClicked()]]></mx:click>
                    </mx:Button>
                    <mx:Button id="helpBtn" x="{reportBtn.x + reportBtn.width + getIconsOffset()}"  y="14"  toolTip="{resourceManager.getString('hm','hm.tooltip.button.manual')}"
                               width="{Math.min(27 * this.width/1280,27)}" maxWidth="27" minWidth="21"
                               height="{Math.min(28 * this.width/1280,28)}" maxHeight="28" minHeight="21"
                               skin="@Embed('/pl/znr/heatmaster/assets/images/icons/help_icon.png')"
                               overSkin="@Embed('/pl/znr/heatmaster/assets/images/icons/help_icon_over.png')"
                               buttonMode="true" visible="true">
                        <mx:click><![CDATA[this.openManualPdf()]]></mx:click>
                    </mx:Button>

                    <mx:Button id="techBtn" x="{helpBtn.x + helpBtn.width + getIconsOffset()}"  y="14"  toolTip="{resourceManager.getString('hm','hm.tooltip.button.how_to')}"
                               width="{Math.min(27 * this.width/1280,27)}" maxWidth="27" minWidth="21"
                               height="{Math.min(28 * this.width/1280,28)}" maxHeight="28" minHeight="21"
                               skin="@Embed('/pl/znr/heatmaster/assets/images/icons/maths_icon.png')"
                               overSkin="@Embed('/pl/znr/heatmaster/assets/images/icons/maths_icon_over.png')"
                               buttonMode="true" visible="true">
                        <mx:click><![CDATA[this.openTechPdf()]]></mx:click>
                    </mx:Button>


                    <mx:Button id="aboutBtn" x="{techBtn.x + techBtn.width + getIconsOffset()}"  y="14"  toolTip="{resourceManager.getString('hm','hm.tooltip.button.about')}"
                               width="{Math.min(27 * this.width/1280,27)}" maxWidth="27" minWidth="21"
                               height="{Math.min(28 * this.width/1280,28)}" maxHeight="28" minHeight="21"
                               skin="@Embed('/pl/znr/heatmaster/assets/images/icons/us_icon.png')"
                               overSkin="@Embed('/pl/znr/heatmaster/assets/images/icons/us_icon_over.png')"
                               buttonMode="true" visible="true">
                        <mx:click><![CDATA[this.openAboutPdfFile()]]></mx:click>
                    </mx:Button>
                </s:Group>

                <mx:Image id="blurringImg" x="0" y="0" height="100%" width="100%" maintainAspectRatio="false"
                          source="@Embed('/pl/znr/heatmaster/assets/images/start/przyciemniacz_start.png')" />

                <s:Group id="logoGroup" x="0" y="0" width="100%" visible="false">
                    <mx:Image width="{Math.max(1024,this.width )}" height="47" maintainAspectRatio="false" x="0" y="0" id="houseTopBarWhiteImg"
                              source="@Embed('/pl/znr/heatmaster/assets/images/top_bar_white.png')"/>
                    <s:Group id="iconLogoGroup" x="5" y="5" >
                        <mx:Button x="0" y="-5" toolTip="FER" buttonMode="true" id="ferLogoImgBtn" skin="@Embed('/pl/znr/heatmaster/assets/images/logo/FER_LOGO1.png')" click="goToFER()"/>
                        <mx:Button  visible="true" id="arkaImg" x="{ferLogoImgBtn.x + ferLogoImgBtn.width + 5}" buttonMode="true" toolTip="ARKA" click="goToARKA()" skin="@Embed('/pl/znr/heatmaster/assets/images/logo/LOGO_ARKA_JPG.jpg')" />
                        <mx:Button  visible="true" id="nfosImg" x="{arkaImg.x + arkaImg.width + 10}" buttonMode="true" toolTip="NFOSiGW" click="goToNFOS()" skin="@Embed('/pl/znr/heatmaster/assets/images/logo/logo_nfosigw.jpg')" />
                        <mx:Button  visible="true" id="wfosImg" x="{nfosImg.x + nfosImg.width + 18}"  buttonMode="true" toolTip="WFOSiGW" click="goToWFOS()" skin="@Embed('/pl/znr/heatmaster/assets/images/logo/logo_wfosigw_katowice.jpg')" />
                    </s:Group>

                    <s:RichText id="headerText" right="15" y="23" fontSize="9" visible="true" textAlign="right" >
                        <s:textFlow>
                            <s:TextFlow>
                                <s:p>HeatMaster został opracowany w ramach kampanii "Ubierz Swój Dom" dofinansowanej ze środków Narodowego Funduszu</s:p>
                                <s:p>Ochrony Środowiska i Gospodarki Wodnej oraz Wojewódzkiego Funduszu Ochrony Środowiska i Gospodarki Wodnej w Katowicach</s:p>
                            </s:TextFlow>
                        </s:textFlow>
                    </s:RichText>

                </s:Group>


                <s:Group x="0" y="0" id="introGroup" width="100%" height="100%">

                    <s:Group y="150" x="{introGroup.width/2 - introContentGroup.width/2}" width="613" height="600" id="introContentGroup">

                        <s:Group id="startBtnContainer" width="100%" height="241" y="55">

                            <s:Image x="0" y="0" id="imgBckgStart" source="@Embed('/pl/znr/heatmaster/assets/images/start/bkg_start1.png')"/>

                            <mx:Button id="englishFlagBtn" y="6" x="10" skin="@Embed('/pl/znr/heatmaster/assets/images/flags/FlagEnglish.jpg')" buttonMode="true">
                                <mx:click><![CDATA[this.setEnglishLang()]]></mx:click>
                            </mx:Button>
                            <mx:Button id="polishFlagBtn" y="6" x="35" skin="@Embed('/pl/znr/heatmaster/assets/images/flags/FlagPL.png')" buttonMode="true">
                                <mx:click><![CDATA[this.setPolishLang()]]></mx:click>
                            </mx:Button>

                            <s:RichEditableText x="{imgBckgStart.x + 10}" y="25" id="introText"  editable="false" focusEnabled="false" selectable="false"
                                                textAlign="left" verticalAlign="middle"  fontSize="18" fontWeight="bold">
                                <s:textFlow>
                                    {TextFlowUtil.importFromString(resourceManager.getString('hm','hm.label.main.title'))}
                                </s:textFlow>
                            </s:RichEditableText>


                            <mx:Button id="startBtn" left="{startBtnContainer.width/2 - 95}" visible="{startButtonVisible}" y="{startBtnContainer.height * 0.8}"
                                       skin="@Embed('/pl/znr/heatmaster/assets/images/start/blank_button.png')" buttonMode="true" label="START" color="white" fontSize="15" fontWeight="bold" textRollOverColor="gray">
                                <mx:click><![CDATA[this.newCalculation()]]></mx:click>
                            </mx:Button>

                            <s:HGroup horizontalAlign="center" verticalAlign="middle" y="{startBtn.y}" x="{introText.x}" width="{introContentGroup.width * 0.9}" visible="{!startButtonVisible}">

                                <mx:Button id="continueBtn"
                                           skin="@Embed('/pl/znr/heatmaster/assets/images/start/blank_button.png')"
                                           buttonMode="true" label="{resourceManager.getString('hm','hm.button.continue')}" color="white" fontSize="15"
                                           fontWeight="bold" textRollOverColor="gray">
                                    <mx:click><![CDATA[
                                            this.continueCalculation()
                                        ]]></mx:click>
                                </mx:Button>

                                <mx:Button id="newCalcBtn"
                                           skin="@Embed('/pl/znr/heatmaster/assets/images/start/blank_button.png')"
                                           buttonMode="true" label="{resourceManager.getString('hm','hm.button.new_calc')}" color="white" fontSize="15"
                                           fontWeight="bold" textRollOverColor="gray" >
                                    <mx:click><![CDATA[
                                            this.newCalculation()
                                        ]]></mx:click>
                                </mx:Button>
                            </s:HGroup>
                        </s:Group>

                        <s:Group width="100%" y="0" id="introLogoGroup">
                            <s:layout>
                                <s:HorizontalLayout/>
                            </s:layout>
                            <s:Spacer width="100%"/>
                            <s:Image source="@Embed('/pl/znr/heatmaster/assets/images/start/LogoHM_big.png')"/>
                            <s:Spacer width="100%"/>
                        </s:Group>

                    </s:Group>
                </s:Group>

                <s:Label id="versionText" text="{resourceManager.getString('hm','hm.label.version')}" left="5" bottom="5" fontSize="9" color="white" click="toggleLocale()"/>
            </s:BorderContainer>
        </s:Group>

    </s:Scroller>

    <mx:Image maintainAspectRatio="false" id="bottomBarImg" left="0" right="0" y="715"  height="{Math.max(this.height - 715,0)}"
              source="@Embed('/pl/znr/heatmaster/assets/images/bottom_bar.png')"/>

</s:Application>
